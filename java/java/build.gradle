apply plugin: 'application'
apply plugin: 'protobuf'

description = "grpc Examples"

repositories {
    mavenCentral()
    mavenLocal()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.9.1'
    }
}

// Adds the 'protocompiler' configuration to dependencies
configurations {
    protocompiler
}

dependencies {
    compile 'io.grpc:grpc-all:0.1.0-SNAPSHOT',
            'org.glassfish:javax.json:1.0.4'
    protocompiler('io.grpc:grpc-compiler:0.1.0-SNAPSHOT') {
        transitive = false
    }
}

task unzipCompiler(type: Copy) {
    into file("${project.buildDir}/compiler/")
    from {
        configurations.protocompiler.collect {
            zipTree(it)
        }
    }
}

protobufCodeGenPlugins = ["java_plugin:${project.buildDir}/compiler/java_plugin"]
generateProto.dependsOn unzipCompiler

task routeGuideServer(type: JavaExec) {
    main = "io.grpc.examples.routeguide.RouteGuideServer"
    description = "Executes the route guide server."
    classpath = sourceSets.main.runtimeClasspath
}

task routeGuideClient(type: JavaExec) {
    main = "io.grpc.examples.routeguide.RouteGuideClient"
    description = "Executes the route guide client."
    classpath = sourceSets.main.runtimeClasspath
}

task helloWorldServer(type: JavaExec) {
    main = "io.grpc.examples.helloworld.HelloWorldServer"
    description = "Executes the hello world server."
    classpath = sourceSets.main.runtimeClasspath
}

task helloWorldClient(type: JavaExec) {
    main = "io.grpc.examples.helloworld.HelloWorldClient"
    description = "Executes the hello world client."
    classpath = sourceSets.main.runtimeClasspath
}
