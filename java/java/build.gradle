apply plugin: 'application'
apply plugin: 'com.google.protobuf'

description = "grpc Examples"

repositories {
    mavenCentral()
    mavenLocal()
}

[compileJava, compileTestJava].each() {
    it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
    it.options.encoding = "UTF-8"
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
          url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.4.1'
    }
}

sourceSets.main.proto.plugins {
    grpc {}
}

dependencies {
    compile 'io.grpc:grpc-all:0.7.0',
            'org.glassfish:javax.json:1.0.4'
}

protocDep = "com.google.protobuf:protoc:3.0.0-alpha-2"
protobufNativeCodeGenPluginDeps = ["grpc:io.grpc:protoc-gen-grpc-java:0.7.0"]

task routeGuideServer(type: JavaExec) {
    main = "io.grpc.examples.routeguide.RouteGuideServer"
    description = "Executes the route guide server."
    classpath = sourceSets.main.runtimeClasspath
}

task routeGuideClient(type: JavaExec) {
    main = "io.grpc.examples.routeguide.RouteGuideClient"
    description = "Executes the route guide client."
    classpath = sourceSets.main.runtimeClasspath
}

task helloWorldServer(type: JavaExec) {
    main = "io.grpc.examples.helloworld.HelloWorldServer"
    description = "Executes the hello world server."
    classpath = sourceSets.main.runtimeClasspath
}

task helloWorldClient(type: JavaExec) {
    main = "io.grpc.examples.helloworld.HelloWorldClient"
    description = "Executes the hello world client."
    classpath = sourceSets.main.runtimeClasspath
}
